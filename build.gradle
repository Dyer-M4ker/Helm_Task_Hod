plugins {
    id 'java'
    id 'application'
    id 'jacoco'
}

group = 'com.example'
version = '1.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

application {
    mainClass = 'com.example.calculator.CalculatorCLI'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

tasks.jar {
    archiveFileName = 'calculator-cli.jar'
    manifest {
        attributes(
                'Main-Class': application.mainClass.get()
        )
    }
}

tasks.named('check') {
    dependsOn tasks.jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('reports/jacocoHtml')
    }
}
